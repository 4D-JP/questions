``WR ON COMMAND``でペースト操作をインターセプトすることができます。

### 解説

ペーストボードには，標準テキスト・HTML・画像など，さまざまなデータタイプを収容することができます。たとえば，Webブラウザ上で画像を「コピー」した場合，一般的には画像と一緒にURLもペーストボードにコピーされます。その場合，テキストエディターのペーストすればURLが転写され，ドローツールなどにペーストすれば画像が転写されることになります。

4D Write上でペースト操作をした場合，ペーストボードに収容されているデータが「画像のみ」であれば「ピクチャをペースト」ダイアログが表示されます。

<img width="526" alt="2017-11-08 12 44 06" src="https://user-images.githubusercontent.com/10509075/32531738-fc017916-c488-11e7-833d-348e396aa050.png">

画面上で「ページにペースト」を選択すると「ピクチャの配置」も指定することができ，「テキストの後ろ」を選択するとテキストの背後に画像がペーストされます。

テキストの背後のペーストされた画像を選択するには，command/controlキーを押しながらクリックします。そのままダブルクリックすれば，「ピクチャプロパティ」ダイアログが表示され，「テキストの後ろ」属性を外すことができます。逆に，テキストの前面にペーストされた画像のプロパティを変更して背後に移動することもできます。

<img width="607" alt="2017-11-08 12 53 54" src="https://user-images.githubusercontent.com/10509075/32531756-1961ad14-c489-11e7-8f21-16356246e5a4.png">

このように配置することができるのは，ペーストの段階で「ページにペースト」された画像だけです。「テキスト中にペースト」した画像をダブルクリック（あるいはコンテキストメニューの「ピクチャ...」を選択）しても，表示される「ピクチャプロパティ」ダイアログには「テキストの後ろ」プロパティが存在しません。4D Writeは，**「テキスト中のピクチャ」と「ページ中のピクチャ」を別々のオブジェクトタイプとして管理している**ためです。

<img width="508" alt="2017-11-08 12 59 47" src="https://user-images.githubusercontent.com/10509075/32531767-286e1fe0-c489-11e7-8eba-4911a2558fd7.png">

なお，4D Writeエリア上で画像をコピーまたはカットした場合，ペーストボードには画像だけでなく，4D Writeオブジェクトとしてのピクチャデータも収納されるため，「ピクチャをペースト」ダイアログは表示されません。

「ピクチャをペースト」ダイアログが表示されるようにするためには，ペースト操作の「前処理」として，ピクチャ以外のデータをペーストボードから取り除くことが必要です。

まず，4D Writeエリアにイベントハンドラーメソッドを登録します。下記の例では，何であれイベントが発生したときには，``WR_ON_COMMAND_CALL``メソッドを実行するようにセットアップしています。

```
WR ON COMMAND (area;"WR_ON_COMMAND_CALL")
```

イベントハンドラーメソッドは，イベントが発生したエリアの参照番号と発生したイベントの識別番号が渡されます。それで，まずイベント識別番号を判定し，``wr cmd paste``以外のケースはデフォルトの動作が発生するようにコードを記述します。ペーストボードにピクチャが含まれているかどうかは，``Pasteboard data size``と``Picture data``の特殊な組み合わせで判断することができます。ペーストボードの内容は``CLEAR PASTEBOARD``で消去できるので，あらかじめピクチャだけをローカル変数に代入しておきます。あとは，このピクチャを``SET PICTURE TO PASTEBOARD``でセットするだけですが，この機会に他の処理もすることができます（任意）。

たとえば，コピーされたピクチャは不必要にサイズがおおきいかもしれないので，``CREATE THUMBNAIL``でリサイズできるかもしれません。アスペクト比率を維持するには``Scaled to fit proportional``オプションを指定します。なお，このコマンドは，指定したサイズよりも元画像が小さかった場合は何もしません。つまり，拡大はしません。

さらに，コピーされたピクチャには，``BMP``や``TIFF``など，サイズのおおきなフォーマットも含まれているかもしれないので，これが写真だとわかっていれば圧縮率の優れた``JPG``形式に，CG（ビッチマップ）だとわかっていれば品質が損なわれない``PNG``形式を残して他の形式を捨てるのも有効です。``CONVERT PICTURE``は，指定した形式がすでに存在すればそれを残し，存在しなければ作成して他の画像形式を排除する効果があります。``CREATE THUMBNAIL``よりも後に変換コマンドを使用するのがポイントです。v12以降，``JPG``の品質も指定できるようになりました。

```
C_LONGINT($1;$area)
C_LONGINT($2;$command)

$area:=$1
$command:=$2

Case of 
  : ($command=wr cmd paste)

    //ピクチャを含むデータをペーストしようとしている
  If (Pasteboard data size(Picture data)=1)

      //ピクチャ以外のデータを消去する
    GET PICTURE FROM PASTEBOARD($image)
    CLEAR PASTEBOARD

      //最大512x512にリサイズする（タテヨコ比を維持）
    CREATE THUMBNAIL($image;$image;512;512;Scaled to fit proportional)

      //.JPG以外の画像フォーマットをクリアする（品質も90%に下げる）
    CONVERT PICTURE($image;".JPG";0.9)

    SET PICTURE TO PASTEBOARD($image)

  End if 

    WR EXECUTE COMMAND ($area;wr cmd paste)

  Else 

  WR EXECUTE COMMAND ($area;$command)

End case 
```

### まとめ

4D Write（32ビット/プラグイン版）でテキストの背後に画像をペーストするためには，ペーストボードに画像だけがコピーされている状態でペーストを実行することが必要です。ペーストボードに画像以外のデータが含まれているケースに対応するためには，まず，``WR ON COMMAND``でペースト操作をインターセプトするためのメソッドを登録しておき，ペーストボードの内容を「フィルター」してから``WR EXECUTE COMMAND``でペースト処理が実行されるような仕掛けを用意することができます。
